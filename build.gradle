plugins {
	id 'distribution'
	id 'maven-publish'
	id 'com.github.node-gradle.node' version '2.2.4'
}

group = 'io.spring.docresources'
description = 'Spring Documentation Resources'

node {
	version = '15.0.1'
	download = true
}

npm_run_build.dependsOn(npm_install)
distZip.dependsOn npm_run_build

distributions {
	main {
		distributionBaseName = 'spring-doc-resources'
		contents {
			from { "${buildDir}/dist" }
			into '/'
		}
	}
}

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = "https://maven.pkg.github.com/spring-io/spring-doc-resources"
			credentials {
				username = System.getenv("GITHUB_ACTOR")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
	publications {
		maven(MavenPublication) {
			artifact distZip
		}
	}
}
